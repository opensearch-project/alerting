/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    apply from: 'build-tools/repositories.gradle'

    ext {
        opensearch_version = System.getProperty("opensearch.version", "3.4.0-SNAPSHOT")
        buildVersionQualifier = System.getProperty("build.version_qualifier", "")
        isSnapshot = "true" == System.getProperty("build.snapshot", "true")
        // 3.0.0-SNAPSHOT -> 3.0.0.0-SNAPSHOT
        version_tokens = opensearch_version.tokenize('-')
        opensearch_build = version_tokens[0] + '.0'
        plugin_no_snapshot = opensearch_build
        if (buildVersionQualifier) {
            opensearch_build += "-${buildVersionQualifier}"
            plugin_no_snapshot += "-${buildVersionQualifier}"
        }
        if (isSnapshot) {
            opensearch_build += "-SNAPSHOT"
        }
        opensearch_no_snapshot = opensearch_version.replace("-SNAPSHOT","")
        common_utils_version = System.getProperty("common_utils.version", opensearch_build)
        kotlin_version = '2.2.0'
    }

    repositories {
        mavenLocal()
        maven { 
            url "https://ci.opensearch.org/ci/dbc/snapshots/maven/"
            mavenContent {
                snapshotsOnly()
            }
        }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

plugins {
    id 'com.netflix.nebula.ospackage' version "12.0.0"
    id "com.dorongold.task-tree" version "1.5"
    id "de.undercouch.download" version "5.6.0"
}

apply plugin: 'base'
apply plugin: 'jacoco'
apply plugin: 'opensearch.java-agent'
apply from: 'build-tools/merged-coverage.gradle'

// Add explicit repository configuration for ktlint
repositories {
    mavenCentral()
}

// Configure ktlint with explicit repository
configurations {
    ktlint {
        resolutionStrategy {
            force "ch.qos.logback:logback-classic:1.5.19"
            force "ch.qos.logback:logback-core:1.5.19"
        }
    }
}

// Add repository ONLY for ktlint resolution
configurations.ktlint.incoming.beforeResolve {
    repositories.clear()
    repositories {
        mavenCentral()
    }
}

dependencies {
    add("ktlint", "com.pinterest:ktlint:0.45.1") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "alerting/**/*.kt", "elastic-api/**/*.kt", "core/**/*.kt"
    
    // Skip on JDK 25
    onlyIf {
        JavaVersion.current() < JavaVersion.VERSION_25
    }
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "alerting/**/*.kt", "elastic-api/**/*.kt", "core/**/*.kt"
    
    // Skip on JDK 25 until ktlint fully supports it
    onlyIf {
        JavaVersion.current() < JavaVersion.VERSION_25
    }
}

check.dependsOn ktlint

allprojects {
    group = "org.opensearch"
    version = "${opensearch_build}"

    apply from: "$rootDir/build-tools/repositories.gradle"

    configurations.all {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
            force "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlin_version}"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
            force "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"
        }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    plugins.withId('org.jetbrains.kotlin.jvm') {
        compileKotlin.kotlinOptions.jvmTarget = compileTestKotlin.kotlinOptions.jvmTarget = JavaVersion.VERSION_21
        compileKotlin.dependsOn ktlint
    }
    tasks.withType(AbstractArchiveTask).configureEach {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }
}

evaluationDependsOnChildren()

check.dependsOn subprojects*.check

// updateVersion: Task to auto increment to the next development iteration
task updateVersion {
    onlyIf { System.getProperty('newVersion') }
    doLast {
        ext.newVersion = System.getProperty('newVersion')
        println "Setting version to ${newVersion}."
        // String tokenization to support -SNAPSHOT
        ant.replaceregexp(match: opensearch_version.tokenize('-')[0], replace: newVersion.tokenize('-')[0], flags:'g', byline:true) {
            fileset(dir: projectDir) {
                // Include the required files that needs to be updated with new Version
                include(name: "alerting/build.gradle")
            }
        }
        ant.replaceregexp(file:'build.gradle', match: '"opensearch.version", "\\d.*"', replace: '"opensearch.version", "' + newVersion.tokenize('-')[0] + '-SNAPSHOT"', flags:'g', byline:true)
    }
}
