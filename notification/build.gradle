/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

/*
 *   Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *   
 *   Licensed under the Apache License, Version 2.0 (the "License").
 *   You may not use this file except in compliance with the License.
 *   A copy of the License is located at
 *   
 *       http://www.apache.org/licenses/LICENSE-2.0
 *   
 *   or in the "license" file accompanying this file. This file is distributed 
 *   on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either 
 *   express or implied. See the License for the specific language governing 
 *   permissions and limitations under the License.
 */

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencies {
    compileOnly "org.opensearch:opensearch:${opensearch_version}"
    compile "org.apache.httpcomponents:httpcore:4.4.5"
    compile "org.apache.httpcomponents:httpclient:4.5.13"
    compile "com.sun.mail:javax.mail:1.6.2"

    testImplementation "org.opensearch.test:framework:${opensearch_version}"
    testImplementation "org.easymock:easymock:4.0.1"
}

shadowJar {
    relocate 'org.apache.http', 'org.opensearch.notification.repackage.org.apache.http'
    relocate 'org.apache.commons.logging', 'org.opensearch.notification.repackage.org.apache.commons.logging'
    relocate 'org.apache.commons.codec', 'org.opensearch.notification.repackage.org.apache.commons.codec'
    classifier = null
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Jar) { task ->
    task.doLast {
        ant.checksum algorithm: 'md5', file: it.archivePath
        ant.checksum algorithm: 'sha1', file: it.archivePath
        ant.checksum algorithm: 'sha-256', file: it.archivePath, fileext: '.sha256'
        ant.checksum algorithm: 'sha-512', file: it.archivePath, fileext: '.sha512'
    }
}

publishing {
    publications {
        shadow(MavenPublication) {
            project.shadow.component(it)
            groupId = 'org.opensearch'
            artifactId = 'notification'

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Open Distro for Elasticsearch Notification"
                packaging = "jar"
                url = "https://github.com/opendistro-for-elasticsearch/alerting"
                description = "Open Distro for Elasticsearch Notification"
                scm {
                    connection = "scm:git@github.com:opendistro-for-elasticsearch/alerting.git"
                    developerConnection = "scm:git@github.com:opendistro-for-elasticsearch/alerting.git"
                    url = "git@github.com:opendistro-for-elasticsearch/alerting.git"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "amazonwebservices"
                        organization = "Amazon Web Services"
                        organizationUrl = "https://aws.amazon.com"
                    }
                }
            }
        }
    }

    // TODO - enabled debug logging for the time being, remove this eventually
    gradle.startParameter.setShowStacktrace(ShowStacktrace.ALWAYS)
    gradle.startParameter.setLogLevel(LogLevel.DEBUG)
}
